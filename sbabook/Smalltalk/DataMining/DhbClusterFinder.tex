$$\halign{ #\hfil&\quad#\hfil\cr {\sl Class}& {\Large\bf DhbClusterFinder}\cr
{\sl Subclass of }&{\tt DhbIterativeProcess}\cr\noalign{\vskip 1ex}

{\sl Instance variable names:}&\parbox[t]{4 in}{\tt  dataServer dataSetSize minimumRelativeClusterSize }\cr\noalign{\vskip 1ex}}$$


Class methods
{\parskip 1ex\par\noindent}
{\bf new:} {\tt anInteger} {\bf server:} {\tt aClusterDataServer} {\bf type:} {\tt aClusterClass}
\begin{verbatim}
    ^ super new initialize: anInteger server: aClusterDataServer type: 
                                                         aClusterClass
\end{verbatim}



Instance methods
{\parskip 1ex\par\noindent}
{\bf accumulate:} {\tt aVector}
\begin{verbatim}
    ( result at: ( self indexOfNearestCluster: aVector)) accumulate: 
                                                              aVector.
\end{verbatim}
{\bf clusters:} {\tt aCollectionOfClusters}
\begin{verbatim}
    result := aCollectionOfClusters.
\end{verbatim}
{\bf collectChangesAndResetClusters}
\begin{verbatim}
    | hasEmptyClusters changes |
    changes := 0.
    hasEmptyClusters := false.
    result do: 
            [ :each | 
            changes := each changes + changes.
            ( each isInsignificantIn: self)
                ifTrue: 
                    [each centerOn: nil.
                    hasEmptyClusters := true]
                ifFalse: [each reset].
            ].
    hasEmptyClusters 
        ifTrue: [result := result reject: [:each | each 
                                                        isUndefined]].
    ^ changes / 2
\end{verbatim}
{\bf dataServer:} {\tt aClusterDataServer}
\begin{verbatim}
    dataServer := aClusterDataServer.
\end{verbatim}
{\bf evaluateIteration}
\begin{verbatim}
    dataServer reset.
    dataSetSize := 0.
    [ dataServer atEnd]
        whileFalse:[ self accumulate: dataServer next.
                     dataSetSize := dataSetSize + 1.
                    ].
    ^ self collectChangesAndResetClusters
\end{verbatim}
{\bf finalizeIterations}
\begin{verbatim}
    dataServer close
\end{verbatim}
{\bf indexOfNearestCluster:} {\tt aVector}
\begin{verbatim}
    | distance index |
    index := 1.
    distance := ( result at: 1) distanceTo: aVector.
    2 to: result size do:
        [ :n | | x |
          x := ( result at: n) distanceTo: aVector.
          x < distance
            ifTrue: [ distance := x.
                      index := n.
                    ].
        ].
    ^ index
\end{verbatim}
{\bf initialize:} {\tt anInteger} {\bf server:} {\tt aClusterDataServer} {\bf type:} {\tt aClusterClass}
\begin{verbatim}
    self dataServer: aClusterDataServer.
    self clusters: ( (1 to: anInteger) collect: [ :n | aClusterClass 
                                                                new]).
    minimumRelativeClusterSize := 0.
    ^ self
\end{verbatim}
{\bf initializeIterations}
\begin{verbatim}
    dataServer open.
    result 
        do: [:each | each isUndefined ifTrue: [each centerOn: 
                                                     dataServer next]]
\end{verbatim}
{\bf minimumClusterSize}
\begin{verbatim}
    ^ (minimumRelativeClusterSize * dataSetSize) rounded
\end{verbatim}
{\bf minimumRelativeClusterSize:} {\tt aNumber}
\begin{verbatim}
    minimumRelativeClusterSize := aNumber max: 0.
\end{verbatim}
{\bf printOn:} {\tt aStream}
\begin{verbatim}
    aStream nextPutAll: 'Iterations: '.
    iterations printOn: aStream.
    result do: [ :each | aStream cr. each printOn: aStream ].
\end{verbatim}
