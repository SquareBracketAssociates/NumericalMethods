$$\halign{ #\hfil&\quad#\hfil\cr {\sl Class}& {\Large\bf DhbNevilleInterpolator}\cr
{\sl Subclass of }&{\tt DhbLagrangeInterpolator}\cr\noalign{\vskip 1ex}

{\sl Instance variable names:}&\parbox[t]{4 in}{\tt  leftErrors rightErrors }\cr\noalign{\vskip 1ex}}$$


Instance methods
{\parskip 1ex\par\noindent}
{\bf computeDifference:} {\tt aNumber} {\bf at:} {\tt anInteger1} {\bf order:} {\tt anInteger2}
\begin{verbatim}
    | leftDist rightDist ratio |
    leftDist := ( self xPointAt: anInteger1) - aNumber.
    rightDist := (  self xPointAt: ( anInteger1 + anInteger2)) - 
                                                              aNumber.
    ratio := ( ( leftErrors at: ( anInteger1 + 1)) - ( rightErrors 
                           at: anInteger1)) / ( leftDist - rightDist).
    leftErrors at: anInteger1 put: ratio * leftDist.
    rightErrors at: anInteger1 put: ratio * rightDist.

\end{verbatim}
{\bf defaultSamplePoints}
\begin{verbatim}
    ^SortedCollection sortBlock: [ :a :b | a x < b x]

\end{verbatim}
{\bf initializeDifferences:} {\tt aNumber}
\begin{verbatim}
    | size nearestIndex dist minDist |
    size := pointCollection size.
    leftErrors size = size
        ifFalse:[ leftErrors := Array new: size.
                  rightErrors := Array new: size.
                ].
    minDist := ( ( self xPointAt: 1) - aNumber) abs.
    nearestIndex := 1.
    leftErrors at: 1 put: ( self yPointAt: 1).
    rightErrors at: 1 put: leftErrors first.
    2 to: size do:
        [ :n |
          dist := ( ( self xPointAt: n) - aNumber) abs.
          dist < minDist
            ifTrue: [ dist = 0
                        ifTrue: [ ^n negated].
                      nearestIndex := n.
                      minDist := dist.
                    ].
         leftErrors at: n put: ( self yPointAt: n).
         rightErrors at: n put: ( leftErrors at: n).
        ].
    ^nearestIndex

\end{verbatim}
{\bf value:} {\tt aNumber}
\begin{verbatim}
    ^(self valueAndError: aNumber) first

\end{verbatim}
{\bf valueAndError:} {\tt aNumber}
\begin{verbatim}
    | size nearestIndex answer error |
    nearestIndex := self initializeDifferences: aNumber.
    nearestIndex < 0
        ifTrue: [ ^Array with: ( self yPointAt: nearestIndex negated) 
                                                             with: 0].
    answer := leftErrors at: nearestIndex.
    nearestIndex := nearestIndex - 1.
    size := pointCollection size.
    1 to: ( size - 1) do:
        [ :m |
          1 to: ( size - m) do:
            [ :n | self computeDifference: aNumber at: n order: m].
          size - m > ( 2 * nearestIndex)
                ifTrue: [ error := leftErrors at: ( nearestIndex + 1) 
                                                                     ]
                ifFalse:[ error := rightErrors at: ( nearestIndex).
                              nearestIndex := nearestIndex - 1.
                            ].
          answer := answer + error.
        ].
    ^Array with: answer with: error abs

\end{verbatim}

