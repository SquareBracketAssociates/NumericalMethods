$$\halign{ #\hfil&\quad#\hfil\cr {\sl Class}& {\Large\bf DhbSymmetricMatrix}\cr
{\sl Subclass of }&{\tt DhbMatrix}\cr\noalign{\vskip 1ex}
}$$


Class methods
{\parskip 1ex\par\noindent}
{\bf join:} {\tt anArrayOfMatrices}
\begin{verbatim}
    | rows n |
    rows := OrderedCollection new.
    n := 0.
    ( anArrayOfMatrices at: 1) rowsDo:
        [ :each |
          n := n + 1.
          rows add: each, ((anArrayOfMatrices at: 3) columnAt: n) ].
    n := 0.
    ( anArrayOfMatrices at: 2) rowsDo:
        [ :each |
          n := n + 1.
          rows add: ((anArrayOfMatrices at: 3) rowAt: n), each ].
    ^ self rows: rows 
\end{verbatim}
{\bf lupCRLCriticalDimension}
\begin{verbatim}
    ^ 36
\end{verbatim}



Instance methods
{\parskip 1ex\par\noindent}
{\bf crlInverse}
\begin{verbatim}
    | matrices b1 cb1ct cb1 |
    matrices := self split.
    b1 := (matrices at: 1) inverse.
    cb1 := (matrices at: 3) * b1.
    cb1ct := (cb1 productWithTransposeMatrix: (matrices at: 3)) 
                asSymmetricMatrix.
    matrices at: 3 put: (matrices at: 2) * cb1.
    matrices at: 2 put: ((matrices at: 2) accumulateNegated: cb1ct) 
                                                              inverse.
    matrices at: 1 put: ( b1 accumulate: (cb1 
                       transposeProductWithMatrix: (matrices at: 3))).
    (matrices at: 3) negate.
    ^ self class join: matrices
\end{verbatim}
{\bf inverse}
\begin{verbatim}
    ^ (rows size < self class lupCRLCriticalDimension or: 
                                           [ lupDecomposition notNil ]) 
            ifTrue: [ self lupInverse ]
            ifFalse: [ self crlInverse ]
\end{verbatim}
{\bf split}
\begin{verbatim}
    | n b d c |
    n := self largestPowerOf2SmallerThan: rows size.
    ^Array with: (self class rows: ((1 to: n) asVector collect: [ 
                               :k | (rows at: k) copyFrom: 1 to: n]))
              with:(self class rows: (((n+1) to: rows size) 
      asVector collect: [ :k | (rows at: k) copyFrom: (n+1) to: rows 
      size]))
              with: (self class superclass rows: (((n+1) to: rows 
     size) asVector collect: [ :k | (rows at: k) copyFrom: 1 to: n]))
\end{verbatim}

